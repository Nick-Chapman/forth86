
# Down the Forth Rabbit Hole.

I have recently stumbled down the Forth Rabbit hole. It's been an enjoyable few weeks. I seem to rediscover Forth and it's concatenative cousins every decade of my career. Have a bit of a dabble and move on. But this time I went deep.

I implemented my own Forth system. As you do. [Quarter Forth](https://github.com/Nick-Chapman/quarter-forth). Initially following Virgil Dupras's excellent [Tumble forth tutorial](https://tumbleforth.hardcoded.net) which taught me details of the _x86_ architecture and boot process. I was very keen to explore implementation from the ground up; using a high level language (for example Haskell, my favorite) didn't seem to really embrace the spirit of Forth which I was learning about.

Initially I was concerned that by implementing my own Forth system, although fun and instructive, I still wouldn't _get_ the essence of Forth. That would require actually coding _in_ Forth. But soon I realized this worry was not well founded. Implementing a Forth system requires writing _lots_ of Forth code. To begin with (following the tutorial) everything was in x86 Asm. But as my system grew, I started implementing more and more in Forth itself. This is probably obvious to die hard Forth-ists! But I had to discover it myself.

There is something glorious about seeing how much of Forth can be implemented in itself. Perhaps the first step of this _self-hosting_ joy is when you see how the `if`/`then` control structures are not special builtins, but just standard Forth words, marked as `immediate` so they act like macros at compile time. The following definitions would suffice in Quarter Forth:

```
: if
['] 0branch compile, here 0 ,
; immediate

: then
dup here swap - swap !
; immediate
```

Even comment handling does not have to be builtin, but can be defined in Forth itself. For example, the following definition will provide basic support:

```
: (
key [char] ) = if exit then recurse
; immediate

( Woo hoo. Now we have comments )
```

In _Quarter Forth_, the disassembler (`see`) and memory dump tools (`dump`/`xxd`) are written in Forth, and were for me quite substantial pieces of code to develop. Also implemented within Forth is a basic system for buffered line input, with support for backspace editing no less! But even as I was working/struggling to implement these applications within Forth, I knew how much harder it would be to work at the level of x86 Asm.

Of course, the other application which can be implemented within Forth is the Forth text interpreter itself...


### Writing a Forth interpreter in Forth

My implementation of the Forth text interpreter is split into separate definitions, `[` and `:`, for the interpreter and colon-compiler. This isn't essential, but I prefer to avoid the global `state` variable. Here is the implementation of the interpreter:

```
: [
word dup s" ]" s= if drop exit then
dup find dup if swap drop execute recurse
then drop number? if ( number is on stack ) recurse
then ." ** Interpreter: '" type ." ' ?" cr recurse
; immediate
```

This in written in my preferred _loop-less_ style; using `recurse` for tail-recursive iteration.
It also relies on the definition of `[` to be available directly after the `:` rather than waiting for closing `;` (No smudge bit here). The main code path is the `word`-`find`-`execute` loop. There are special cases for the `]` word, which is handled as a terminating marker, rather that being it's own word. Another case for numeric handling, which is tried after a failed dictionary lookup. And then an error handling case for when a word is neither in the dictionary or parse-able as a number.

The interpreter builds on Forth definitions such as `word`, `find`, `s=` and `number?` -- `word` reads a blank-delimited word from the input stream. `find` looks in the dictionary for the execution token associate with a named word. `s=` is string equality. `number?` attempts to parse a string as a numeric literal. -- Ultimately we reach the _really_ basic primitives, such as `key` and `emit` to read individual characters from the input stream and write individual characters to the output stream.

Having the kernel provide implementations for basic operations such as `key` and `emit` seems entirely reasonable. These implementations will likely be tied to specifics of the host architecture (say x86 BIOS calls), but using Forth for the next layer of definitions: `word`, `find`, `s=` and then eventually `[` and `:` seems a nice approach. Forth is way easier to code and understand than Asm. (Not easy, but easier!) But more importantly, the Forth code wont have to rewritten if we re-target out system to a new architecture. Win-Win.

But here we stumble on a problem: how can we process these Forth definitions without already having an interpreter/compiler available?


### The bootstrap problem

Is there any way to bootstrap a Forth system (by which I mean a working interpreter/compiler) without already having a working interpreter/compiler available? Happily, the answer to this question is _YES_. The details of exactly how are explored by _Quarter Forth_.

A first step towards solving this bootstrapping problem is figuring out how to deal with numerics. The Forth interpreter/compiler must be able to recognize numeric literals, and convert from their string representation in a given base. In Quarter Forth this is implemented by the word `number?`. The implementation of `number?` can be written in Forth, and although it requires support for some primitive arithmetic operations, it crucially does _not_ require support for numeric literals; except for a few specific numbers like _10_ and _16_. We can arrange for the kernel to provide us primitives for any numbers required, but it is sufficient for the kernel to provide us just `1` and `0`, and we can build whatever other numbers we need.

```
1 1 +
constant 2

2 1 + dup * 1 +
constant 10
```

We might also conceive how to have the kernel provide us just an interpreter, and build the compiler on top of this, in Forth. The compiler is after all just a convenient way to construct the executable code for a new dictionary definition. With the right primitives, such as `compile,` and `ret,`, we can build dictionary entries within interpreter mode. But even with numerics and the colon-compiler solved, how do we avoid having to still code, in Asm, a fairly rich interpreter, which support the full logic for `word` and `find`?


### Quarter

The solution to the bootstrap problem taken by _Quarter Forth_ is _Quarter_.

_Quarter_ is a spartan version of Forth, where computation is directed at the level of individual characters, rather than blank-delimited words. Instead of `word` and `find`, we have primitives `key` and `dispatch`:
- `key` is the standard primitive to read a single character from the input stream.
- `dispatch` indexes into a flat table of execution tokens using a single character. In essence providing a rudimentary dictionary.

The kernel need only provide a simple _key-dispatch-execute-loop_ instead of the standard interpreter (which could be described as a _word-fetch-execute-loop_).

The code for the _key-dispatch-execute-loop_ looks as follows in my _x86_ implementation, which even with basic error support for a failed dispatch is rather small. (Aside: The underscore prefix is just a convention to indicate an Asm routine which conforms to Forth style, using the parameter stack, not registers, to transmit arguments.)

```
.loop:
    call _key
    call _dispatch
    call _dup
    call _if
    jz .ignore
    call _execute
    jmp .loop
.ignore:
    call _drop
    jmp .loop
```

### Next time

In the next article I will dive deeper into Quarter and Quarter Code.

- What primitives do we need?
- What does it feel like to code in?
- What does Quarter code look like? [peek](https://github.com/Nick-Chapman/quarter-forth/blob/main/f/quarter.q)
- How does it differ from conventional Forth?
